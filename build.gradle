ext.libraries = [
    jdk: '1.7',
    frege_core: 'org.frege-lang:frege:3.24-7.30',
    frege_repl: 'org.frege-lang:frege-repl-core:1.4-SNAPSHOT'
]

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.org.frege-lang:frege-gradle-plugin:0.7"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
    }
}

/* PLUGINS */

apply plugin: 'java'
apply plugin: "org.frege-lang"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.asciidoctor.convert'

/* JDK */

targetCompatibility = libraries.jdk
sourceCompatibility = libraries.jdk

compileFrege {
    target = libraries.jdk
}

/* DEPENDENCIES */

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/groups/public" }
}

dependencies {
    compile libraries.frege_core
    compile libraries.frege_repl
}

/* BINTRAY */

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId    releaseGroup
            artifactId 'barber'
            version    releaseVersion

            from components.java
        }
    }
}

bintray {
 /* Normally taken from ~/.gradle/gradle.properties */
    user         = project.hasProperty('bintrayUser') ? project.bintrayUser : 'bintrayUser'
    key          = project.hasProperty('bintrayKey') ? project.bintrayKey : 'bintrayKey'
    publish      = false
    publications = ['mavenCustom']
    pkg {
        repo            = 'maven'
        name            = 'barber'
        desc            =  releaseDescription
        websiteUrl      = 'http://fregelab.github.io/barber/'
        issueTrackerUrl = 'https://github.com/fregelab/barber/issues'
        licenses        = ['Apache-2.0']
        vcsUrl          = 'https://github.com/fregelab/barber.git'
        labels          = ['frege', 'image']
        version {
            name      = releaseVersion
            desc      = 'Barber Release'
            released  = new Date()
            vcsTag    = releaseVersion
        }
    }
}

/* DOCUMENTATION */

asciidoctor {
    attributes 'source-highlighter': 'prettify',
               toc                 : 'left',
               icons               : 'font',
               toclevels           : 3,
               stylesheet          : 'css/barber.css',
               'pdf-style'         : 'default',
               'release-version'   : releaseVersion,
               'frege-version'     : libraries.frege_core,
               'frege-repl'        : libraries.frege_repl,
               'target-jdk'        : libraries.jdk,
               'barber-version'    : releaseVersion
}
